entrypoints Module, Exp ;

comment "--" ;
comment "{-" "-}" ;

layout "where", "let", "split" ;
layout stop "in" ;
-- Do not use layout toplevel as it makes pExp fail!

Module.   Module ::= "module" AIdent "where" "{" [Imp] [Decl] "}" ;

Import.   Imp ::= "import" AIdent ;
separator Imp ";" ;

DeclDef.    Decl ::= AIdent [Tele] ":" Exp "=" ExpWhere ;
DeclData.   Decl ::= "data" AIdent [Tele] "=" [Label] ;
DeclSplit.  Decl ::= AIdent [Tele] ":" Exp "=" "split" "{" [Branch] "}" ;
DeclUndef.  Decl ::= AIdent [Tele] ":" Exp "=" "undefined" ;
-- DeclMutual. Decl ::= "mutual" "{" [Decl] "}" ;
separator   Decl ";" ;

Where.    ExpWhere ::= Exp "where" "{" [Decl] "}" ;
NoWhere.  ExpWhere ::= Exp ;

Let.          Exp  ::= "let" "{" [Decl] "}" "in" Exp ;
Lam.          Exp  ::= "\\" [PTele] "->" Exp ;
Path.         Exp  ::= "<" [AIdent] ">" Exp ;
CLam.         Exp  ::= "[" [AIdent] "]" Exp ;
Prev.         Exp  ::= "prev" AIdent Exp ;
Fun.          Exp1 ::= Exp2 "->" Exp1 ;
Pi.           Exp1 ::= [PTele] "->" Exp1 ;
Sigma.        Exp1 ::= [PTele] "*" Exp1 ;
Forall.       Exp1 ::= "forall" [AIdent] "," Exp1 ; 
Next.         Exp1 ::= "next" AIdent DelSubst Exp1 System ;
NextEmp.      Exp1 ::= "next" AIdent Exp1 ;
DFix.         Exp1 ::= "dfix" AIdent Exp2 Exp2 ;
Fix.          Exp1 ::= "fix" AIdent AIdent Exp2 Exp2 ;
Later.        Exp2 ::= "|>" AIdent DelSubst Exp2 ;
LaterEmp.     Exp2 ::= "|>" AIdent Exp2 ;
AppFormula.   Exp2 ::= Exp2 "@" Formula ;
App.          Exp2 ::= Exp2 Exp3 ;
CApp.         Exp2 ::= Exp2 "$" AIdent ; 
IdP.          Exp3 ::= "IdP" Exp4 Exp4 Exp4 ;
Comp.         Exp3 ::= "comp" Exp4 Exp4 System ;
Trans.        Exp3 ::= "transport" Exp4 Exp4 ;
Fill.         Exp3 ::= "fill" Exp4 Exp4 System ;
Glue.         Exp3 ::= "glue" Exp4 System ;
GlueElem.     Exp3 ::= "glueElem" Exp4 System ;
Fst.          Exp4 ::= Exp4 ".1" ;
Snd.          Exp4 ::= Exp4 ".2" ;
Pair.         Exp5 ::= "(" Exp "," [Exp] ")" ;
Var.          Exp5 ::= AIdent ;
PCon.         Exp5 ::= AIdent "{" Exp "}" ; -- c{T A B} x1 x2 @ phi
U.            Exp5 ::= "U" ;
Hole.         Exp5 ::= HoleIdent ;
coercions Exp 5 ;
separator nonempty Exp "," ;

Dir0.       Dir ::= "0" ;
Dir1.       Dir ::= "1" ;

System.     System ::= "[" [Side] "]" ;

DelSubst.   DelSubst ::= "[" [DelBind] "]" ;

DelBind.  DelBind ::= "(" AIdent ":" Exp ")" "<-" Exp ;
separator DelBind "," ;


Face.     Face ::= "(" AIdent "=" Dir ")" ;
separator Face "" ;

Side.   Side ::= [Face] "->" Exp ;
separator Side "," ;

Disj.     Formula  ::= Formula "\\/" Formula1 ;
Conj.     Formula1 ::= Formula1 CIdent Formula2 ;
Neg.      Formula2 ::= "-" Formula2 ;
Atom.     Formula2 ::= AIdent ;
Dir.      Formula2 ::= Dir ;
coercions Formula 2 ;

-- Branches
OBranch.   Branch ::= AIdent [AIdent] "->" ExpWhere ;
-- TODO: better have ... @ i @ j @ k -> ... ?
PBranch.   Branch ::= AIdent [AIdent] "@" [AIdent] "->" ExpWhere ;
separator Branch ";" ;

-- Labelled sum alternatives
OLabel.   Label ::= AIdent [Tele] ;
PLabel.   Label ::= AIdent [Tele] "<" [AIdent] ">" System ;
separator Label "|" ;

-- Telescopes
Tele.     Tele ::= "(" AIdent [AIdent] ":" Exp ")" ;
CTele.    Tele ::= AIdent ;
terminator Tele "" ;

-- Nonempty telescopes with Exp:s, this is hack to avoid ambiguities
-- in the grammar when parsing Pi
PTele.    PTele ::= "(" Exp ":" Exp ")" ;
terminator nonempty PTele "" ;

position token AIdent ('_')|(letter)(letter|digit|'\''|'_')* ;
separator AIdent "" ;

token CIdent '/''\\' ;

position token HoleIdent '?' ;